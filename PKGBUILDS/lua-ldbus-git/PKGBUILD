# Maintainer: Calvin Timmer <calvin.timmer@mailbox.org>

_basename=ldbus
_pkgname=lua-${_basename}
pkgname=${_pkgname}-git
pkgver=master
pkgrel=1
pkgdesc='A C binding to dbus for Lua'
arch=('any')
url='https://github.com/daurnimator/ldbus'
license=('MIT')
depends=('libdbus' 'lua>=5.3' 'lua<5.4')
makedepends=('git')
provides=("${_pkgname}")
conflicts=('lua-ldbus')
source=("${_pkgname}::git+https://github.com/daurnimator/${_basename}.git")
sha256sums=('SKIP')

prepare()
{
  cd "${srcdir}/${_pkgname}"

  # Remove compat-5.3 dependency
  sed -i -e '\|^#include "compat-5.3.h"$|d' src/*.c
  sed -i -e '\|^CFLAGS += -I ../vendor/compat-5.3/c-api$|d' src/Makefile
  sed -i -e 's| ../vendor/compat-5.3/c-api/compat-5.3.o||' src/Makefile

  # Change lua package name for pkg-config to lua53
  sed -i -re 's|(PKG_CONFIG.*) lua5.3|\1 lua53|' src/Makefile
}

pkgver()
{
  cd "${srcdir}/${_pkgname}"
  printf "r%s.%s\n" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build()
{
  cd "${srcdir}/${_pkgname}"

  make --directory=src
}

package()
{
  cd "${srcdir}/${_pkgname}"

  # Install license
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"

  # Install documentation
  install -D -m644 README.md "${pkgdir}/usr/share/doc/${_pkgname}/README.md"

  # Install module
  install -D -m644 src/${_basename}.so "${pkgdir}/usr/lib/lua/5.3/${_basename}.so"
  install -D -m644 src/message/variant.lua "${pkgdir}/usr/share/lua/5.3/${_basename}/message/variant.lua"
}
