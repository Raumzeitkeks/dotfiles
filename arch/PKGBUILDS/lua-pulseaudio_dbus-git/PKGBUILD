# Maintainer: Calvin Timmer <calvin.timmer@mailbox.org>

_basename=pulseaudio_dbus
_pkgname=lua-${_basename}
pkgname=${_pkgname}-git
pkgver=master
pkgrel=1
pkgdesc='This Lua module provides a simple API to control PulseAudio using DBus'
arch=('any')
url='https://github.com/stefano-m/lua-pulseaudio_dbus'
license=('Apache')
depends=('lua-ldbus_api' 'lua>=5.3' 'lua<5.4')
makedepends=('git')
provides=("${_pkgname}")
install="${pkgname}.install"
source=("${_pkgname}::git+https://github.com/stefano-m/${_pkgname}.git")
sha256sums=('SKIP')

pkgver()
{
  cd "${srcdir}/${_pkgname}"
  printf "r%s.%s\n" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package()
{
  cd "${srcdir}/${_pkgname}"

  # Install documentation
  install -D -m644 README.md "${pkgdir}/usr/share/doc/${_pkgname}/README.md"

  # Install module
  install -D -m644 ${_basename}.lua "${pkgdir}/usr/share/lua/5.3/${_basename}.lua"
}
