# Maintainer: Calvin Timmer <calvin.timmer@mailbox.org>
# Contributor: Florian Dejonckheere <florian at floriandejonckheere dot be>
# Contributor: jyantis <yantis@yantis.net>
# Contributor: Christian Rebischke <Chris.Rebischke[at]archlinux[dot]org>

_pkgname=powerlevel9k
pkgname=${_pkgname}-git
pkgver=master
pkgrel=1
pkgdesc='A theme for ZSH which uses Powerline Fonts'
arch=('any')
url='https://github.com/bhilburn/powerlevel9k'
license=('MIT')
depends=('zsh')
makedepends=('git')
optdepends=(
  'acpi: battery monitoring'
  'git: display git repository status'
  'mercurial: display hg repository status'
  'ttf-font-awesome: icon font'
  'awesome-terminal-fonts: icon font package'
  'powerline-fonts: patched fonts for powerline'
  'oh-my-zsh-git: oh-my-zsh integration'
  'prezto-git: Prezto integration'
  'antigen-git: Antigen integration'
  'zpm: ZPM integration'
  'zsh-zim-git: Zim integration')
conflicts=('zsh-theme-powerlevel9k' 'zsh-theme-powerlevel9k-git')
source=("${_pkgname}::git+https://github.com/bhilburn/${_pkgname}.git")
sha256sums=('SKIP')

pkgver()
{
  cd "${srcdir}/${_pkgname}"
  printf "r%s.%s\n" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package()
{
  cd "${srcdir}/${_pkgname}"

  # Install license
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"

  # Install documentation
  install -D -m644 README.md "${pkgdir}/usr/share/doc/${_pkgname}/README.md"

  # Install the theme
  install -D -m644 powerlevel9k.zsh-theme "${pkgdir}/usr/share/${_pkgname}/${_pkgname}.zsh-theme"
  for FILE in functions/*.zsh; do
    install -D -m644 "${FILE}" "${pkgdir}/usr/share/${_pkgname}/functions/$(basename ${FILE})"
  done
}
