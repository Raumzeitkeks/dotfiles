# Maintainer: Calvin Timmer <calvin.timmer@mailbox.org>

_basename=pulseaudio_widget
_pkgname=awesome-${_basename}
pkgname=${_pkgname}-git
pkgver=master
pkgrel=1
pkgdesc='This widget for AwesomeWM is a wrapper around the pulseaudio_dbus library'
arch=('any')
url='https://github.com/stefano-m/awesome-pulseaudio_widget'
license=('GPL3')
depends=('awesome' 'lua-pulseaudio_dbus')
makedepends=('git')
provides=("${_pkgname}")
source=("${_pkgname}::git+https://github.com/stefano-m/${_pkgname}.git")
sha256sums=('SKIP')

pkgver()
{
  cd "${srcdir}/${_pkgname}"
  printf "r%s.%s\n" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package()
{
  cd "${srcdir}/${_pkgname}"

  # Install documentation
  install -D -m644 README.md "${pkgdir}/usr/share/doc/${_pkgname}/README.md"

  # Install module
  install -D -m644 ${_basename}.lua "${pkgdir}/usr/share/awesome/lib/${_basename}.lua"
}
